; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "WTL Helper"
!define PRODUCT_VERSION "1.1.6"
!define PRODUCT_PUBLISHER "Sergey Solozhentsev"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

SetCompressor LZMA

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
;!define MUI_LICENSEPAGE_RADIOBUTTONS
;!insertmacro MUI_PAGE_LICENSE "license.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "WTL Helper"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "WTLHelper.exe"
InstallDir "$PROGRAMFILES\WTL Helper"
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
	Push $0
	Push $1
	ReadRegStr $0 HKLM "Software\SaloS\WtlHelper\Common" "DdxTypesFile"
	StrLen $1 $0
	IntCmp $1 0 SkipDeleteOld SkipDeleteOld
	MessageBox MB_ICONINFORMATION|MB_OK "You should uninstall previous version of WTL Helper"
	Abort
SkipDeleteOld:
	Pop $1
	Pop $0
FunctionEnd

Section "WTL Helper" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "Release\WtlHelper.dll"
  RegDLL "$INSTDIR\WtlHelper.dll"
  File "Release2005\WtlHelper8.dll"
  RegDLL "$INSTDIR\WtlHelper8.dll"
  File "messages.xml"
  File "common.xml"
  SetOutPath "$INSTDIR\1033"
  File "Release\1033\WtlHelperRes.dll"
  WriteRegStr HKLM "Software\SaloS\WtlHelper\Common" "RegMessages" "$INSTDIR\Messages.xml"
  WriteRegStr HKLM "Software\SaloS\WtlHelper\Common" "CommonFile" "$INSTDIR\common.xml"
;Visual studio 2003
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "CommandLineSafe" 0
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "CommandPreload" 1
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "LoadBehavior" 3
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "Description" "Class Wizard for WTL"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "FriendlyName" "WTL Helper"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "SatelliteDLLPath" "$INSTDIR"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "SatelliteDLLName" "WtlHelperRes.dll"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect" "AboutBoxDetails" "WTL Helper is powerful tool that helps using WTL."
  WriteRegDWORD HKCU "SOFTWARE\Microsoft\VSA\7.1\PreloadAddinState" "WtlHelper.Connect" 1
  WriteRegDWORD HKCU "SOFTWARE\Microsoft\VisualStudio\7.1\PreloadAddinState" "WtlHelper.Connect" 1
;Visual Studio 2005  
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "CommandLineSafe" 0
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "CommandPreload" 1
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "LoadBehavior" 3
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "Description" "Class Wizard for WTL"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "FriendlyName" "WTL Helper"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "SatelliteDLLPath" "$INSTDIR"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "SatelliteDLLName" "WtlHelperRes.dll"
  WriteRegStr HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8" "AboutBoxDetails" "WTL Helper is powerful tool that helps using WTL."
  WriteRegDWORD HKCU "SOFTWARE\Microsoft\VSA\8.0\PreloadAddinState" "WtlHelper.Connect8" 1
  WriteRegDWORD HKCU "SOFTWARE\Microsoft\VisualStudio\8.0\PreloadAddinState" "WtlHelper.Connect8" 1
; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -AdditionalIcons
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  IfSilent silent_end
  MessageBox MB_ICONINFORMATION|MB_OK "Uninstall of $(^Name) was successfully completed."
silent_end:
FunctionEnd

Function un.onInit
  IfSilent +3
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure that, you want to delete $(^Name)?" IDYES +2
  Abort
  Push $R1
  FindWindow $R1 "wndclass_desked_gsk" ""
  IntCmp $R1 0 notfindMSVS
findMSVS:
  MessageBox MB_ICONINFORMATION|MB_YESNO "Micrsoft Development Environment is running.$\r$\nClose it and then press Yes$\r$\nPress No if you want to Abort uninstalation" IDYES +2
  Abort
  FindWindow $R1 "wndclass_desked_gsk" ""
  IntCmp $R1 0 notfindMSVS findMSVS findMSVS
notfindMSVS:
  Pop $R1
FunctionEnd

Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\messages.xml"
  Delete "$INSTDIR\common.xml"
  
  UnRegDLL "$INSTDIR\WtlHelper8.dll"
  Delete "$INSTDIR\WtlHelper8.dll"
  DeleteRegKey HKLM "SOFTWARE\Microsoft\VisualStudio\7.1\Addins\WtlHelper.Connect"
  UnRegDLL "$INSTDIR\WtlHelper.dll"
  Delete "$INSTDIR\WtlHelper.dll"
  
  Delete "$INSTDIR\1033\WtlHelperRes.dll"
  
  
  DeleteRegKey HKLM "SOFTWARE\Microsoft\VisualStudio\8.0\Addins\WtlHelper.Connect8"
  DeleteRegKey HKLM "Software\SaloS\WtlHelper"

  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"

  RMDir "$SMPROGRAMS\$ICONS_GROUP"
  RMDir "$INSTDIR\Messages"
  RMDir "$INSTDIR\1033"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd
